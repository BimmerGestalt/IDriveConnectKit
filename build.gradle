import de.undercouch.gradle.tasks.download.Download
import de.undercouch.gradle.tasks.download.Verify

buildscript {
    ext.kotlin_version = '1.4.21'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        //classpath group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'
        //classpath group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.2.6'
    }
}

plugins {
    id 'com.github.kt3k.coveralls' version '2.8.2'
}
apply from: 'buildtools/ColoredOutput.gradle'
apply from: 'buildtools/jacoco.gradle'
apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

if (!project.getPlugins().hasPlugin("de.undercouch.download")) {
    apply plugin: "de.undercouch.download"
}

repositories {
    mavenCentral()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

sourceSets {
    main.java.srcDirs += 'build/generated'
    test.java.srcDirs += 'src/test/java'
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

task downloadEtchCompiler(type: Download) {
    src 'http://archive.apache.org/dist/etch/1.4.0/apache-etch-1.4.0-windows-x86-bin.zip'
    dest file("${buildDir}/etch/apache-etch-1.4.0-windows-x86-bin.zip")
    overwrite false
    outputs.file "${buildDir}/etch/apache-etch-1.4.0-windows-x86-bin.zip"
}
task verifyEtchCompiler(type: Verify, dependsOn: downloadEtchCompiler) {
    src file("${buildDir}/etch/apache-etch-1.4.0-windows-x86-bin.zip")
    algorithm "SHA-512"
    checksum "915128A2E6E6FA83F4A576EBAC4DA5A250EC6F3BEB11C76404664B5067CFD94703531219" +
             "6CAB6BB6EF96EEB765E39A87E11E774A59257E7932F2D3761CAE0354"
}
task extractEtchCompiler(type: Copy, dependsOn: verifyEtchCompiler) {
    from zipTree(file("${buildDir}/etch/apache-etch-1.4.0-windows-x86-bin.zip"))
    into "${buildDir}/etch"
    outputs.dir "${buildDir}/etch/apache-etch-1.4.0"
}
task compileEtch(type: JavaExec, dependsOn: extractEtchCompiler) {
    main "org.apache.etch.compiler.EtchMain"
    classpath files(
            "${buildDir}/etch/apache-etch-1.4.0/lib/apache-etch-compiler-1.4.0.jar",
            "${buildDir}/etch/apache-etch-1.4.0/lib/apache-etch-java-compiler-1.4.0.jar",
            "${buildDir}/etch/apache-etch-1.4.0/lib/velocity-1.7-dep.jar"
    )
    workingDir 'etch'
    args('--binding', 'java', '--output-dir', "${buildDir}/generated", 'BMWRemoting.etch')
    inputs.file("etch/BMWRemoting.etch")
    outputs.dir "${buildDir}/generated"
}
task enableEtchRuntime(type: Copy, dependsOn: extractEtchCompiler) {
    from "${buildDir}/etch/apache-etch-1.4.0/binding-java/lib"
    include '*.jar'
    into file("libs/")
}
task extractEtchRuntime(type: Copy, dependsOn: enableEtchRuntime) {
    from zipTree(file("libs/apache-etch-java-runtime-1.4.0.jar"))
    include '**/*.class'
    includeEmptyDirs false
    into "${buildDir}/classes/java/main"
    outputs.dir "${buildDir}/classes/java/main/org/apache/etch"
}

compileJava.dependsOn compileEtch
compileJava.dependsOn extractEtchRuntime
compileJava.mustRunAfter compileEtch
compileJava.mustRunAfter extractEtchRuntime

// https://youtrack.jetbrains.com/issue/IDEA-119280
task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
}
task copyTestResourcesOut(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/../out/test/classes"
}
// doesn't seem to actually work, you should run this step manually
processTestResources.dependsOn copyTestResources
processTestResources.dependsOn copyTestResourcesOut

publishing {
    publications {
        maven(MavenPublication) {
            // publish to Jitpack
            groupId 'io.bimmergestalt'
            artifactId 'IDriveConnectKit'
            version '0.2'

            from components.java
        }
    }
}